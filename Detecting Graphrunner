//Detecting GraphRunner in Azure Environments

dataset = msft_azure_ad_raw
//Device Code Authentication to Microsoft Graph
| filter (
    authenticationProtocol  = "deviceCode" 
    and resourceDisplayName  = "Microsoft Graph"
)
//GraphRunner Default User-Agent Patterns
or (
    userAgent contains "Mozilla/5.0 (iPhone; CPU iPhone OS 13_2_3 like Mac OS X) AppleWebKit/605.1.15" or
    userAgent contains "Mozilla/5.0 (iPhone; CPU iPhone OS 8_3 like Mac OS X) AppleWebKit/600.1.4" or
    userAgent contains "Mozilla/5.0 (Linux; Android 6.0; Build/MRA58N) AppleWebKit/537.36" or
    userAgent contains "Mozilla/5.0 (Linux; Android 8.1.0; Pixel Build/OPM4.171019.021.D1) AppleWebKit/537.36" or
    userAgent contains "Mozilla/5.0 (Linux; U; Android 4.0.2; en-us; Galaxy Nexus Build/ICL53F) AppleWebKit/534.30" or
    userAgent contains "Mozilla/5.0 (Macintosh; Intel Mac OS X 10_14_6) AppleWebKit/537.36" or
    userAgent contains "Mozilla/5.0 (Macintosh; Intel Mac OS X 10_14_6) AppleWebKit/605.1.15" or
    userAgent contains "Mozilla/5.0 (Macintosh; Intel Mac OS X 10_15_7) AppleWebKit/537.36" or
    userAgent contains "Mozilla/5.0 (Windows NT 10.0; Win64; x64) AppleWebKit/537.36" or
    userAgent contains "Mozilla/5.0 (Windows NT 10.0; WOW64; rv:70.0) Gecko/20101101 Firefox/70.0" or
    userAgent contains "Mozilla/5.0 (Windows NT 10.0; WOW64; Trident/7.0; rv:11.0) like Gecko"
)
| alter has_device_code = if(authenticationProtocol = "deviceCode", true, false)
| alter has_graphrunner_ua = if(
        userAgent contains "Mozilla/5.0 (iPhone" or
        userAgent contains "Mozilla/5.0 (Linux; Android" or
        userAgent contains "Mozilla/5.0 (Macintosh" or
        userAgent contains "Mozilla/5.0 (Windows NT",
        true,
        false
)
| alter detection_type = if(
    has_device_code = true and has_graphrunner_ua = true,
    "HIGH_CONFIDENCE",
    if(has_device_code = true, "DEVICE_CODE_AUTH", "SUSPICIOUS_USER_AGENT")
)
| fields _time, detection_type, userPrincipalName, authenticationProtocol, resourceDisplayName, ipAddress, location, userAgent
| sort desc _time
