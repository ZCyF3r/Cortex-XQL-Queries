//Detect brute force attempts from event logs

dataset = xdr_data 
| filter event_type = ENUM.EVENT_LOG 
| filter action_evtlog_event_id = 4625
| alter TargetUserName = action_evtlog_data_fields -> TargetUserName
| alter WorkstationName = action_evtlog_data_fields -> WorkstationName
| alter LogonType = action_evtlog_data_fields -> LogonType
| alter IpAddress = action_evtlog_data_fields -> IpAddress
| alter TargetDomainName = action_evtlog_data_fields -> TargetDomainName
| comp count() as failed_count, max(_time) as latest_failure
    by TargetUserName, TargetDomainName, WorkstationName, IpAddress 
| join conflict_strategy = both (
    dataset = xdr_data
    | filter event_type = ENUM.EVENT_LOG 
    | filter action_evtlog_event_id = 4624
    | alter TargetUserName = action_evtlog_data_fields -> TargetUserName
    | alter WorkstationName = action_evtlog_data_fields -> WorkstationName
    | alter LogonType = action_evtlog_data_fields -> LogonType
    | alter IpAddress = action_evtlog_data_fields -> IpAddress
    | alter TargetDomainName = action_evtlog_data_fields -> TargetDomainName
    | alter success_time = _time
) as successful_login
    successful_login.TargetUserName = TargetUserName and 
    successful_login.WorkstationName = WorkstationName and
    successful_login.IpAddress = IpAddress and 
    successful_login.TargetDomainName = TargetDomainName 
| filter timestamp_diff(success_time, latest_failure, "second") > 0 and
    timestamp_diff(success_time, latest_failure, "second") < 6000
| fields _time, TargetUserName, TargetDomainName, WorkstationName, IpAddress, failed_count, latest_failure, success_time
